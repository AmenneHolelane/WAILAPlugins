buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies { 
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT' 
    }
}
plugins {
    id 'net.franz-becker.gradle-lombok' version '1.5'
    id 'com.matthewprenger.cursegradle' version '1.0.8'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'


group = 'tterrag.wailaplugins'
archivesBaseName = "WAILAPlugins"
version = "MC${minecraft_version}-${mod_version}-${System.getenv().BUILD_NUMBER}"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = 'run'
    mappings = 'stable_29'
    
    replace "@VERSION@", project.version
	replace "${toReplace}", "required-after:Forge@[${forge_version},);required-after:endercore@[${endercore_version},);required-after:Waila;"
    replaceIn "WailaPlugins.java"
}

repositories {
    maven { // ttCore
        name 'tterrag Repo'
        url "http://maven.tterrag.com/"
    }
	maven { // WAILA
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven/"
    }
	maven { // CCC/NEI (for WAILA)
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
    maven { // Forestry
        name 'Player Maven'
        url "http://maven.ic2.player.to/"
    }
    maven { // RCrops
        name 'TehNut Maven'
        url "http://tehnut.info/maven/"
    }
    maven { // JEI
        url "http://dvs1.progwml6.com/files/maven/"
    }
    ivy {
        name = "RailCraft"
        url = "http://www.railcraft.info/ivy/"
    }
    maven { // CurseForge
    	url "https://minecraft.curseforge.com/api/maven/"
    }
}

dependencies {
    deobfCompile "com.enderio.core:EnderCore:${endercore_version}"
	deobfCompile "mcp.mobius.waila:Hwyla:${HWYLA_version}_${minecraft_version}"
    deobfCompile "net.sengir.forestry:forestry_${minecraft_version}:${forestry_version}"
    deobfCompile "info.tehnut.resourcefulcrops:ResourcefulCrops:${minecraft_version}-${rcrops_version}"
    deobfCompile "com.wayoftime.bloodmagic:BloodMagic:${minecraft_version}-${bloodmagic_version}"
    deobfCompile "info.amerifrance.guideapi:Guide-API:${minecraft_version}-${guideapi_version}"
    
    deobfCompile "thermalexpansion:ThermalExpansion-${minecraft_version}:${te_version}:universal"
    deobfCompile "thermal-foundation:ThermalFoundation-${minecraft_version}:${tf_version}:universal"
    deobfCompile "cofhcore:CoFHCore-${minecraft_version}:${cofhcore_version}:universal"
    deobfCompile "codechicken-lib-1-8:CodeChickenLib-${minecraft_version}:${ccl_version}:universal"
    
    deobfCompile ("com.headlamp-games:Railcraft:${railcraft_version}") {
        exclude module: "forestry_1.10.2"
        exclude module: "industrialcraft-2"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

lombok {
    version = "1.16.6"
    sha256 = "e0a471be03e1e6b02bf019480cec7a3ac9801702bf7bf62f15d077ad4df8dd5d"
}

import net.franz_becker.gradle.lombok.task.DelombokTask

task delombok(type: DelombokTask) {
    args("src/main/java", "-d", "build/sources/delomboked/java")
}

tasks.eclipse.dependsOn installLombok


tasks.curseforge.enabled = System.getenv('CURSE') != null && System.getenv('CURSE').equals('true') && project.hasProperty('curseforge_key')
    
curseforge {
    if (project.hasProperty('curseforge_key')) {
        apiKey = project.curseforge_key
    }

    project {
        id = project.curse_projectId
        changelog = System.getenv('CHANGELOG') == null || System.getenv('CHANGELOG').equals('none') ? getChangelog() : System.getenv('CHANGELOG')
        changelogType = 'text'
        releaseType = 'beta'
		addGameVersion '1.10.2'
        mainArtifact(jar) {
            displayName = "WAILA Plugins - ${version}"
            relations {
                requiredLibrary 'endercore'
                requiredLibrary 'hwyla'
            }
        }
    }
}
